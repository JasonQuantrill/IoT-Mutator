// Rules 1 & 4

rule "Irrigation - calculate whether to start watering"
when
    Time cron "0 * * ? * *" // every minute
then
	try {
		logInfo(logName, "Calculating whether to start irrigation")

		// calculate rainfall
		SumRainLast24h.sendCommand((WeatherAndForecastCurrentRain.sumSince(now.minusHours(24), "influxdb") as Number).doubleValue)
		SumRainNext24h.sendCommand(GroupForecastedRain24Sum.state as Number)

		// wait to propagate item states - not sure if necessary
		Thread.sleep(200)

		logDebug(logName, "Rain - last 24h (sum): {} mm", String::format("%.2f", (SumRainLast24h.state as Number).doubleValue))
		logDebug(logName, "Rain - forecast 24h (sum): {} mm", String::format("%.2f", (SumRainNext24h.state as Number).doubleValue))

		///////////////////		
		// start calculations, whether to start and for how long
		///////////////////

		// check for the manual lock
		if (IrrigationLock.state == ON) {
			logInfo(logName, "Irrigation lock is on")
			return
		}

		// check the week day
		val Number day = now.getDayOfWeek()
		val dayItem = ScriptServiceUtil.getItemRegistry.getItem("IrrigationDay" + day)
		
		if (dayItem === null || dayItem.state == OFF || dayItem.state == NULL) {
			logInfo(logName, "Inappropriate day to start irrigation", dayItem)
			return
		}

		// set the default irrigation hour to X hours before the sunrise
		val localSunRise = new DateTime(LocalSunRiseStart.state.toString).minusHours((IrrigationHoursBeforeSunrise.state as Number).intValue)
		var Number wateringHour = localSunRise.getHourOfDay()
		var Number wateringMinute = localSunRise.getMinuteOfHour()

		// if there is a specific hour in settings, then use it
		if (IrrigationStartAtSpecificHour.state == ON) {
			wateringHour = IrrigationStartTime.state as Number
			wateringMinute = 0
		}

		logInfo(logName, "Watering at: {}:{}", wateringHour, wateringMinute)

		// check if the current time is the watering time (full hour)
		if (now.getHourOfDay != wateringHour || now.getMinuteOfHour != wateringMinute) {
			// nope - good bye
			logInfo(logName, "Inappropriate time to start irrigation")
			return
		}
		logInfo(logName, "It is watering hour: {}:{}", wateringHour, wateringMinute)

		// check if the current wind speed is higher then the max allowed
		logInfo(logName, "Current wind speed: {} km/h", String::format("%.2f", (WeatherAndForecastCurrentWindSpeed.state as Number).doubleValue))
		if (WeatherAndForecastCurrentWindSpeed.state > MaxAllowedWindSpeed.state as Number) {
			logInfo(logName, "Wind speed too high to irrigate")
			return
		}

		// if the rainfall sum for the last 24h and the forecast for 24h is higher then set, then we are not going to irrigate
		val rainSum = (SumRainLast24h.state as Number).doubleValue + (SumRainNext24h.state as Number).doubleValue
		logInfo(logName, "Past and forcasted average rain: {} mm", String::format("%.2f", rainSum))
		if (rainSum > (MaxAllowedRain.state as Number).doubleValue) {
			logInfo(logName, "To heavy rain to irrigate (past and forcasted)")
			return
		}

		// check the wether, and based on that set the watering time coefficient factor
		// if the temperature is to low, don't start watering
		val avgTemperatureLast24h = (WeatherAndForecastCurrentTemperature.averageSince(now.minusHours(24), "influxdb") as Number).doubleValue

		logInfo(logName, "Average temperature for the last 24h: {}", avgTemperatureLast24h)

		if (avgTemperatureLast24h <= 10) {
			logInfo(logName, "Temperature too low to start irrigation")
			return
		} 
		else if (avgTemperatureLast24h > 30) {
			logInfo(logName, "Setting irrigation coefficient factor to 2")
		} 
		else {
			// coefficient factor should be between 1 and 2
			// this part could, and should be better
			coefficientFactor = avgTemperatureLast24h / 10 - 1;
			logInfo(logName, "Setting irrigation coefficient factor to {}", coefficientFactor)
		}

		///////////////////
		// ok, let's start watering, cascading all of the valves from the GroupIrrigationValves
		///////////////////

		// starting with the Zone 1, other zones will be turned on in sequence by the separate rule
		logInfo(logName, "Starting the irrigation sequence")
		IrrigationCurrentValve.sendCommand(IrrigationValveZone1.name)
	}
	catch (Exception e) {
        logError(logName, "Error calculating whether to start irrigation: " + e)
    }
end


rule "Irrigation - all valves closed"
when
	Item GroupIrrigationValves changed to OFF
then
	// set the current valve to OFF
	logInfo(logName, "All irrigation valves closed")
	IrrigationCurrentValve.postUpdate(OFF)

	// reset the remaining time
	IrrigationSectionRemainingTime.postUpdate(0)
end